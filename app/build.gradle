apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

def versionMajor = 0
def versionMinor = 2
def versionPatch = 0

repositories {
    flatDir { dirs 'libs' }
}

android {
    compileSdkVersion toolVersions.android.compileSdk

    defaultConfig {
        applicationId "com.lex.simplequest"
        minSdkVersion toolVersions.android.minSdk
        targetSdkVersion toolVersions.android.targetSdk
        versionCode 1
        versionName "${versionMajor}.${versionMinor}.${versionPatch}(${versionCode})"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    viewBinding {
        enabled = true
    }

    buildTypes {
        debug {
            minifyEnabled false
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

android.applicationVariants.all { variant ->
    variant.outputs.all { output ->
        def apkFileName = output.outputFileName
        if (apkFileName.endsWith("apk") && !apkFileName.contains("unaligned")) {
            def project = "simplequest"
            def separator = "-"
            def buildType = variant.buildType.name
            def versionCode = ((versionMajor + 1) * 1_000_000) + (versionMinor * 1_000) + (versionPatch)
            def versionName = "${versionMajor}.${versionMinor}.${versionPatch}(${versionCode})"
            def apkName = project + separator + versionName + separator + buildType + ".apk"

            output.outputFileName = apkName
            output.versionNameOverride = versionName
            output.versionCodeOverride = versionCode
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation(name: 'mvp-android-2.0.0-SNAPSHOT-release', ext: 'aar')
    implementation deps.support.appCompat
    implementation deps.support.coreKtx
    implementation deps.support.constraintLayout
    implementation deps.rxAndroid
    implementation deps.rxJava
    implementation deps.viewPump
    implementation deps.support.design
    implementation deps.okHttp.okHttp
    implementation deps.okHttp.loggingInterceptor
    implementation deps.retrofit.retrofit
    implementation deps.retrofit.converterGson
    implementation deps.picasso
    implementation deps.playservices.location
    implementation deps.playservices.gcm

    //implementation 'com.aldoapps:autoformatedittext:0.9.3'
//    implementation 'me.abhinay.input:currency-edittext:1.1'
//    implementation 'com.cottacush:CurrencyEditText:0.0.3'

    implementation project(':core')

    // TODO: Wrap with project-properties
    implementation 'com.google.android.gms:play-services-maps:17.0.0'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    debugImplementation 'com.amitshekhar.android:debug-db:1.0.6'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
